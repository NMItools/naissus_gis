include "mapbasic.def"
include "menu.def"
include "icons.def"
include "Library\ERRORLib.def"
include "nmiWin.def"
include "nmiFunkcije.def"
include "nmiDotNet.def"
include "NAISSUS-GIS.def"

'===================================================================================================================== 
'	Geodetski snimljeni objekti NIVOS-a

SUB NIVOS_GEODETSKI
	
	onerror goto Greska 
	
	Create Menu "NIVOS Geodetski" as
		"Koordinate" calling Koordinate,
		"(-",
		"Kote cevovoda - KC" calling KotaCevi,
		"Kote terena - KT" calling KotaTerena,
		"KC + KT" calling KCiKT,	
		"Dubina cevi" calling DubinaCevi
	

	Alter Menu "JKP Naissus" Add 
		"NIVOS Geodetski" As "NIVOS Geodetski"	
		
	'---
	  
	Create ButtonPad "NIVOS Geodetski" As
	
		ToolButton 
			Calling Koordinate
			ID 991
			Icon MI_ICON_CROSSHAIR
			Cursor MI_CURSOR_ARROW
			DrawMode DM_CUSTOM_POINT
			HelpMsg "Ocitavanje koordinata\nKoordinate"	
			
		SEPARATOR
		
		ToggleButton Calling KotaTerena
			ID 501
			Icon 8479 File "nmiIcons.dll"
			HelpMsg "Prikaz kota terena\nKota terena"
			Uncheck
			Enable 	
		ToggleButton Calling KotaCevi 
			ID 502
			Icon 8481 File "nmiIcons.dll"
			HelpMsg "Prikaz kota cevovoda\nKota Cevovoda"
			Uncheck
			Enable 	 
		ToggleButton Calling KCiKT
			ID 503
			Icon 8109 File "nmiIcons.dll"
			HelpMsg "Prikaz kota terena i kota cevi\nKota terena i kota cevi"
			Uncheck
			Enable 
		
		SEPARATOR
		
		ToggleButton Calling DubinaCevi
			ID 504
			Icon 8483 File "nmiIcons.dll"
			HelpMsg "Prikaz dubine cevovoda\nDubina cevi"
			Uncheck
			Enable 		
	
	'---
		
	Alter ButtonPad "NIVOS Geodetski" 
	'	Position(8,5.5) Units "cm"
		ToolbarPosition (1,5)
		Fixed
		Title "NIVOS Geodetski"
		Width 21
		Show        

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "NIVOS_GEODETSKI")
	Call ERRShow()	               

End SUB   

'===================================================================================================================== 
' .....

SUB KotaCevi

	onerror goto Greska 
	
	Alter Button ID 592 Uncheck
	Alter Button ID 593 Uncheck
	Alter Button ID 594 Uncheck
	
	namLejer=setTabSQL("SNIMLJENO_KOTE",501)
	
	Set Map redraw off
	Set Map Layer LayerID("SNIMLJENO_KOTE") Label Font ("Arial",258,8,61680,0) With Left$(Str$(Kota_cevi), Int(Kota_cevi>0)*Len(Str$(Kota_cevi))) Position Below Right Visibility Zoom (0,250) Units "m" Overlap Off  Auto Retry On Auto On 
	Set Map redraw on

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "KotaCevi")
	Call ERRShow()	               

End Sub

'===================================================================================================================== 
' .....

sub KotaTerena

	onerror goto Greska 
	
	Alter Button ID 591 Uncheck
	Alter Button ID 593 Uncheck
	Alter Button ID 594 Uncheck

	namLejer=setTabSQL("SNIMLJENO_KOTE",502)
	
	Set Map redraw off
	Set Map Layer LayerID("SNIMLJENO_KOTE") Label Font ("Arial",258,8,12648288,0) With Left$(Str$(Kota_terena), Int(Kota_terena>0)*Len(Str$(Kota_terena))) Position Above Right Visibility Zoom (0,249) Units "m" Overlap Off  Auto Retry On Auto On
	Set Map redraw on

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "KotaTerena")
	Call ERRShow()	               

End Sub
 
'===================================================================================================================== 
' .....

sub KCiKT

	onerror goto Greska 

	Alter Button ID 591 Uncheck
	Alter Button ID 592 Uncheck
	Alter Button ID 594 Uncheck
	
	namLejer=setTabSQL("SNIMLJENO_KOTE",503)
	
	set map redraw off
	Set Map Layer LayerID("SNIMLJENO_KOTE") Label Font ("Arial",258,8,0,16777215) With Left$(Str$(Kota_terena), Int(Kota_terena>0)*Len(Str$(Kota_terena)))+Chr$(13)+Left$(Str$(Kota_cevi), Int(Kota_cevi>0)*Len(Str$(Kota_cevi))) Position Right Visibility Zoom (0,249) Units "m" Overlap Off  Auto Retry On Auto On
	set map redraw on

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "KCiKT")
	Call ERRShow()	               

End Sub

'===================================================================================================================== 
' .....

sub DubinaCevi
	
	onerror goto Greska

	Alter Button ID 591 Uncheck
	Alter Button ID 592 Uncheck
	Alter Button ID 593 Uncheck
	
	namLejer=setTabSQL("SNIMLJENO_KOTE",504)
	
	set map redraw off
	Set Map Layer LayerID("SNIMLJENO_KOTE") Label Font ("Arial",2,8,13658112,16777215) With Left$(Str$(Dubina_cevi), Int(Dubina_cevi<0)*Len(Str$(Dubina_cevi))) Position Right Visibility Zoom (0,249) Units "m" Overlap Off  Auto Retry On Auto On
	set map redraw on

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "DubinaCevi")
	Call ERRShow()	               

End Sub

'===================================================================================================================== 
' .....

Sub Koordinate
	
	onerror goto Greska	
	
	Dim x,y as float
	Dim objTmp As Object
'	Dim CosmName as String

	Print CHR$(12)
	Print "======================================================="
	Print "    Koordinate taèaka                                  "
' - Postavljanje poèetnog koordinatnog sistema
	CRS = SetCRS()		
	
' - Kreiranje pomoænog sloja za unos koordinata	
	Call KoordinateTest

' - Broj veæ unetih taèaka u tabeli
	tck = TableInfo(s_KoordTab, TAB_INFO_NROWS)
	
' - Oèitavanje koordinata na mesto klika

	x = CommandInfo(CMD_INFO_X)
	y = CommandInfo(CMD_INFO_Y)	
	
' - Kreiranje taèke	
	objTmp = CreatePoint (x,y)
	tck = tck + 1

' - unos taèke u Cosmetic sloj
'	CosmName = CosmeticIme("c")
'	Insert Into CosmName (Obj) Values (objTmp)

' - unos taèke u tabelu
	Insert Into s_KoordTab (Tacka, Obj) Values (tck, objTmp)
	
' - Ispisivanje koordinata srpskih koordinatnih zona

'	"Longitude / Latitude (WGS 84) - EPSG: 4326"	
	Set CoordSys Earth Projection 1, 104 Bounds (21.579058000, 42.928281000) (22.602521000, 43.522997000)
	Print "-------------------------------------------------------"
	Print CoordSysName$("CoordSys Earth Projection 1, 104 Bounds (21.579058000, 42.928281000) (22.602521000, 43.522997000)")
	Print "x = " & CentroidX(objTmp) & " / " & DMS(CentroidX(objTmp))
	Print "y = " & CentroidY(objTmp) & " / " & DMS(CentroidY(objTmp))
	Update s_KoordTab Set COL2 = CentroidX(objTmp) , COL3 = CentroidY(objTmp), COL4 = DMS(CentroidX(objTmp)), COL5 = DMS(CentroidY(objTmp)) Where RowID = tck
	
'	"UTM Zone 34, Northern Hemisphere (WGS 84) - EPSG: 32634"
	Set CoordSys Earth Projection 8, 104, 7, 21, 0, 0.9996, 500000, 0
	Print "-------------------------------------------------------"
	Print CoordSysName$("CoordSys Earth Projection 8, 104, 7, 21, 0, 0.9996, 500000, 0")
	Print "x = " & CentroidX(objTmp)
	Print "y = " & CentroidY(objTmp)
	Update s_KoordTab Set COL6 = CentroidX(objTmp), COL7 = CentroidY(objTmp) Where RowID = tck

'	"Drzavni Koordinatni Sistem - Gauss Krüger 7. zona (JKP ''Naissus'' tr.) - EPSG: 31277"	
    CRS = SetCRS()
	Print "-------------------------------------------------------"
	Print CoordSysName$(s_CRS)
	Print "x = " & CentroidX(objTmp)
	Print "y = " & CentroidY(objTmp)
	Update s_KoordTab Set COL8 = CentroidX(objTmp), COL9 = CentroidY(objTmp) Where RowID = tck
	
'	"Web GK7 (geomreze.rgz.gov.rs) - EPSG: 32805"	
	Set CoordSys Earth Projection 8, 9999, 10, 578.8172, 165.104, 390.20938, -4.91963, 0.98852, 13.038, -7.80238, 0, 7, 21, 0, 0.9999, 7500000, 0
'	Print "-------------------------------------------------------"
'	Print CoordSysName$("CoordSys Earth Projection 8, 9999, 10, 578.8172, 165.104, 390.20938, -4.91963, 0.98852, 13.038, -7.80238, 0, 7, 21, 0, 0.9999, 7500000, 0")
'	Print "x = " & CentroidX(objTmp)
'	Print "y = " & CentroidY(objTmp)
	Update s_KoordTab Set COL10 = CentroidX(objTmp), COL11 = CentroidY(objTmp) Where RowID = tck

'	"Web mercator (Google/Bing/OSM) - EPSG: 3857"	
	Set CoordSys Earth Projection 10, 157, 7, 0
'	Print "-------------------------------------------------------"
'	Print CoordSysName$("CoordSys Earth Projection 10, 157, 7, 0")
'	Print "x = " & CentroidX(objTmp)
'	Print "y = " & CentroidY(objTmp)
	Update s_KoordTab Set COL12 = CentroidX(objTmp), COL13 = CentroidY(objTmp) Where RowID = tck

'	"ETRS-TM34 - EPSG: 25834"	
	Set CoordSys Earth Projection 2024, 115, 7, 21, 0, 0.9996, 500000, 0, 0, 0, 1500000, 8000000
'	Print "-------------------------------------------------------"
'	Print "EPSG 25834 Lon: "+CentroidX(objTmp)
'	Print "EPSG 25834 Lat: "+CentroidY(objTmp)
	Update s_KoordTab Set COL14 = CentroidX(objTmp), COL15 = CentroidY(objTmp) Where RowID = tck
	
'	"ETRS TM Zone 34, Northern Hemisphere (ETRS89) - EPSG: 3046"	
	Set CoordSys Earth Projection 8, 115, 7, 21, 0, 0.9996, 500000, 0
'	Print "-------------------------------------------------------	
'	Print "EPSG 3046 Lon: "+CentroidX(objTmp)
'	Print "EPSG 3046 Lat: "+CentroidY(objTmp)
	Update s_KoordTab Set COL16 = CentroidX(objTmp), COL17 = CentroidY(objTmp) Where RowID = tck
	
'	"Longitude / Latitude (EUREF89) - EPSG: 4258"	
	Set CoordSys Earth Projection  1, 115
'	Print "-------------------------------------------------------	
'	Print "EPSG 4326 DMS Lon: "+CentroidX(objTmp)
'	Print "EPSG 4326 DMS Lat: "+CentroidY(objTmp)
	Update s_KoordTab Set COL18 = CentroidX(objTmp), COL19 = CentroidY(objTmp) Where RowID = tck

'	Izlazak iz programa uz setovanje default koordinatnog sistema
	CRS = SetCRS()	
	Exit Sub
	
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "Koordinate")
	Call ERRShow()	    	

End Sub


