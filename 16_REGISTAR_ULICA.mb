include "mapbasic.def"
include "menu.def"
include "icons.def"
include "Library\ERRORLib.def"
include "nmiWin.def"
include "nmiFunkcije.def"
include "nmiDotNet.def"
include "NAISSUS-GIS.def"

'===================================================================================================================== 
'	Alati za rad sa adresnim registrom (unos novih ulica i povezivanje sa parcelama)

Sub REGISTAR_ULICA
	
	onerror goto Greska

	Create Menu "Registar ulica" As
		"Registar ulica" HelpMsg "Submenu Item" Calling DLG_REGISTAR_ULICA,
		"Izaberi ulicu za unos..." HelpMsg "Submenu Item" Calling ULICA_IZBOR,
		"Dodeli naziv ulice parceli" HelpMsg "Submenu Item" Calling ULICA_PARCELA,
		"Ukloni naziv ulice parceli" HelpMsg "Submenu Item" Calling ULICA_UKLONI

	Alter Menu "JKP Naissus" Add 
		"Registar ulica" As "Registar Ulica",
		"(-"
	
	Create ButtonPad "Registar ulica" As
			
		PushButton
			Calling DLG_REGISTAR_ULICA
			Icon MI_ICON_ADDTOLIBRARY
			HelpMsg "Registar Ulica (Izmena, Unos, Brisanje)\nRegistar Ulica"
		
		SEPARATOR
		
		PushButton
			Calling ULICA_IZBOR
			Icon MI_ICON_COMPASS_EXPAND
			HelpMsg "Aktiviranje ulice za unos\nAktiviranje ulice za unos"
			
		ToolButton Calling ULICA_PARCELA
			ID 311
			Icon MI_ICON_MAPSYMB_5
			Cursor MI_CURSOR_ARROW
			DrawMode DM_CUSTOM_POINT
			HelpMsg "Dodeli ulicu parcelama\nDodeli ulicu parcelama"
		
		SEPARATOR
		
		ToolButton Calling ULICA_UKLONI
			ID 312
			Icon MI_ICON_SIGNS_11
			Cursor MI_CURSOR_ARROW
			DrawMode DM_CUSTOM_POINT
			HelpMsg "Ukloni ulicu sa parcela\nUkloni ulicu sa parcela"

	
	Alter ButtonPad "Registar ulica" 
		Position(12,6) Units "cm"
'		ToolbarPosition (1,4)
		Float
		Title "Registar ulica"
		Width 25
		Show


	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "REGISTAR_ULICA")
	Call ERRShow()	 	
	
End Sub

Sub arr_KO		

	dim n as smallint
	
	OnError goto Greska
	
' - Spisak KO u niz
	s_kom = "SELECT [MATICNI_BROJ], [NAZIV_LATIN2] FROM [GIS].[kn].[ADMJ_NASELJA_KO] ORDER BY [NAZIV_LATIN2]"
	
' - Izvrsavanje SQL StoredProc/query 
	i_hstmt = Server_Execute(i_hdbc, s_kom) 
		Server i_hstmt Bind Column 1 To i_MBKO, am_stat
		Server i_hstmt Bind Column 2 To s_KO, am_stat
		Server i_hstmt Fetch First

' - Kreiraj niz od svih KO = arr_KO() 
		n=1
		While Not Server_EOT(i_hstmt)

			ReDim arr_MBKO(n)
			arr_MBKO(n) = i_MBKO

			ReDim arr_KO(n)
			arr_KO(n) = s_KO

			Server i_hstmt Fetch Next
			n=n+1
		Wend
		
	Print "---------------------------------------------------"
	Print "Pronadjeno je "+(n-1)+" Katastarskih Opstina"
	
	Server i_hstmt Close		
	
	Exit Sub
'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "arr_KO")
	Call ERRShow()
	
End Sub

Sub arr_NAZIV_ULICE(byVal s as integer)
	
	dim n as smallint
	
	OnError goto Greska
	
'	Print "arr_NAZIV_ULICE"
'	print "s = " + s
	
	sn_KO = arr_KO(s)
	sn_MBKO = arr_MBKO(s)
	
' - Spisak ULICA u odabranoj KO 
	s_kom = "SELECT [ID_IME_UL], [NAZIV_LATIN2] FROM [GIS].[data].[kn_ulice_nazivi] WHERE MAT_BR_KO = " + sn_MBKO + " ORDER BY [NAZIV_LATIN2]"
'	print s_kom

' - Izvrsavanje SQL StoredProc/query 
	i_hstmt = Server_Execute(i_hdbc, s_kom) 
		Server i_hstmt Bind Column 1 To i_ID_IME_UL, am_stat
		Server i_hstmt Bind Column 2 To s_NAZIV_LATIN2, am_stat
		Server i_hstmt Fetch First

' - Kreiraj niz od svih naziva lica u zadatoj KO
		
		n=1
		While Not Server_EOT(i_hstmt)

			ReDim arr_ID_UL(n)
			arr_ID_UL(n) = i_ID_IME_UL

			ReDim arr_NAZIV_ULICE(n)
			arr_NAZIV_ULICE(n) = s_NAZIV_LATIN2

			Server i_hstmt Fetch Next
			n=n+1
		Wend
	
	If n-1 = 0 
		Then ReDim arr_NAZIV_ULICE(1)
		arr_NAZIV_ULICE(1) = "Nema ulica za ovu KO!"
	End If
	
	Print "---------------------------------------------------"
	Print "Pronadjeno je "+(n-1)+" ulica u Katastarskoj Opstini " + arr_KO(s)
	
	Server i_hstmt Close	
	
	Exit Sub
'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "arr_NAZIV_ULICE")
	Call ERRShow()
	
End Sub

Sub pop_arr_NAZIV_ULICE

	OnError goto Greska
	
'	Print "pop_arr_NAZIV_ULICE"
	
	Do Case ReadControlValue(2)
		
	Case ReadControlValue(2) = 1

		Alter Control 7 Hide
		Alter Control 7 Disable

	Case ReadControlValue(2) = 2
		
		Call arr_NAZIV_ULICE(ReadControlValue(4))
		Alter Control 7 Show
		Alter Control 7 Enable
		Alter Control 7 Title from Variable arr_NAZIV_ULICE() 
		Alter Control 7 Value 1

	End Case
	
	sn_KO = arr_KO(ReadControlValue(4))
	sn_MBKO = arr_MBKO(ReadControlValue(4))
	
'	print sn_KO + " / "+ sn_MBKO
	
	Exit Sub
'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "pop_arr_NAZIV_ULICE")
	Call ERRShow()
	
End Sub

Sub btn_REGISTAR_ULICA_azuriraj
	
	OnError goto Greska
	
	Dim s_latin1, s_latin2, s_cir, s_kat_ops, metod as string
	Dim i_id, i_mbko, i_return_val as integer
	
	If ReadControlValue(9) = ""
	Then
		Note "Prazno polje za naziv ulice!"
		Exit Sub
	End If
	
	Do Case ReadControlValue(2)
	
		' - UNOS NOVOG NAZIVA ULICE
		
		Case  ReadControlValue(2) = 1
		
			metod = "INSERT"
			i_id = 0
			s_latin1 = ReadControlValue(9)
			s_latin2 = ReadControlValue(9)
			s_cir = "-"
			i_mbko = sn_MBKO
			s_kat_ops = sn_KO
	
		' - AŽURIRANJE POSTOJEÆEG NAZIVA ULICE (BEZ AŽURIRANJA OPŠTINA!)

		Case  ReadControlValue(2) = 2
			
			metod = "UPDATE"
			i_id = arr_ID_UL(ReadControlValue(7))
			s_latin1 = ReadControlValue(9)
			s_latin2 = ReadControlValue(9)
			s_cir = "-"
			i_mbko = 0
			s_kat_ops = "-"
	
	End Case
	
' - Priprema t-SQL komande	
	s_kom = "DECLARE @return_value int;EXEC @return_value = [mbx].[sp_ulice_azuriranje] @metod = N'" + metod + "', @id_ul = " + i_id + ", @lat1 = N'" + s_latin1 + "', @lat2 = N'" + s_latin2 + "', @cir = N'" + s_cir + "', @br_ko = " + i_mbko + ", @ko = N'" + s_kat_ops + "';SELECT	'Return Value' = @return_value INTO MI_user.retval"
'	print s_kom

	' - Izvršavanje SQL StoredProc/query 
'	Server i_hdbc Begin Transaction
	i_hstmt = Server_Execute(i_hdbc, s_kom)
	Server i_hstmt Close
	i_hstmt = Server_Execute(i_hdbc, "SELECT * from MI_user.retval")
		Server i_hstmt Bind Column 1 To i_return_val, am_stat
		Server i_hstmt Fetch First
	Server i_hstmt Close
	
	
	If i_return_val = 0 
	Then 
'		Server i_hdbc Commit
		i_hstmt = Server_Execute(i_hdbc, "DROP TABLE [MI_user].[retval]")
		Server i_hstmt Close
		Note "Ažuriranje uspešno."
	Else
'		Server i_hdbc Rollback
		Note "Došlo je do greške - ažuriranje neuspešno!"
		print "Neuspešna t-SQL komanda: "
		print s_kom
	End If
	
	If OtvorenTAB("kn_ulice_nazivi") = True
	Then
		Server Refresh kn_ulice_nazivi
	End If		
	

	
	Exit Sub
'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "btn_REGISTAR_ULICA_azuriraj")
	Call ERRShow()
		
End Sub

Sub ULICA_IZBOR

	onerror goto Greska  
		
	Alter Button ID 311 Uncheck
	
'	Iz selekcije izvuèi ID ulice i ime
	Fetch First From Selection
	s_Ulica_ID = kn_ulice_nazivi.ID_IME_UL
	s_Ulica_Ime = str$(kn_ulice_nazivi.NAZIV_LATIN2)
	Print "Ume ulice " & kn_ulice_nazivi.NAZIV_LATIN2 & " aktivno za povezivanje sa parcelom"
	Run Menu Command M_QUERY_UNSELECT

	Exit Sub
	'	-------------------------
	Greska:
'	Print "Da li je odabrano ime ulice za unos u tabeli?"
	Call ERRCreate(Err(), Error$(), "Da li je odabrano ime ulice za unos u tabeli?")
	Call ERRShow()

	
End Sub

Sub ULICA_PARCELA

	onerror goto Greska  
	
	Dim x, y, x2, y2 As Float,
		i, i_found, i_rowid As Integer,
		s_table As Alias,
		KO as String,
		o_parc as Object
	
	CRS = SetCRS()

'	stil za parcelu	koja je završena
	b_parcela = MakeBrush(2,16752800,16711680)
	p_parcela = MakePen (2,2,16711680) 
	
	iMapWindowID = WindowID(FrontWindow())
	
	' - Odredjivanje koordinata gde je kliknuo korisnik.
	x = CommandInfo(CMD_INFO_X)
	y = CommandInfo(CMD_INFO_Y)
	
	KO = PronadjiKO("ADMJ_NASELJA_KO", "NAZIV_LATIN2", x ,y)
	
	i_found = SearchPoint(FrontWindow( ), x, y)

	If i_found = 0 Then
			Note "Ne postoji parcela na lokaciji!"
		Else
	' - Procesiranje rezultata
		For i = 1 to i_found
		'	ime tabele koja je "pogodjena" ispod klika.
			s_table = SearchInfo(i, SEARCH_INFO_TABLE)
		'	pronadji row ID objekta koji je kliknut.
			i_rowid = SearchInfo(i, SEARCH_INFO_ROW)
			
			print i_found + " (" + s_table + "/" + i_rowid + ")"
			
			If s_table = "KATASTAR_PARCELE" Then
			'	Fetch-uj redove za sve atribute
				Fetch rec i_rowid From s_table
				as_broj_Parcele = s_table + ".col1"
				as_mat_broj_KO = s_table + ".col7"
				o_parc = KATASTAR_PARCELE.obj
				
				Print "Pronaðena parcela broj " + as_broj_Parcele + " (" + KO + " / " + as_mat_broj_KO + ")"
				
				Update KATASTAR_PARCELE Set ULICA = s_Ulica_ID Where Rowid = i_rowid
				Alter Object o_parc Info OBJ_INFO_BRUSH, b_parcela
				Alter Object o_parc Info OBJ_INFO_PEN, p_parcela
				Update KATASTAR_PARCELE Set obj = o_parc Where Rowid = i_rowid				
				
				Print "Ulica  povezana..."

				Exit For
			End If
		Next		
		
	End If

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "ULICA_PARCELA")
	Call ERRShow()

End Sub

Sub ULICA_UKLONI

	onerror goto Greska  
	
'	Note"Ukloni ulicu"
	
	Dim x, y, x2, y2 As Float,
		i, i_found, i_rowid As Integer,
		s_table As Alias,
		KO as String,
		o_parc as Object
	
	CRS = SetCRS()

'	defautl stil za parcelu
	b_parcela_def = MakeBrush(17,65280, -1)
	p_parcela_def = MakePen(1,2,65280)
	
	iMapWindowID = WindowID(FrontWindow())
	
	' - Odredjivanje koordinata gde je kliknuo korisnik.
	x = CommandInfo(CMD_INFO_X)
	y = CommandInfo(CMD_INFO_Y)
	
	i_found = SearchPoint(FrontWindow( ), x, y)

	If i_found = 0 Then
			Note "Ne postoji parcela na lokaciji!"
		Else
	' - Procesiranje rezultata
		For i = 1 to i_found
		'	ime tabele koja je "pogodjena" ispod klika.
			s_table = SearchInfo(i, SEARCH_INFO_TABLE)
		'	pronadji row ID objekta koji je kliknut.
			i_rowid = SearchInfo(i, SEARCH_INFO_ROW)
			
			print i_found + " (" + s_table + "/" + i_rowid + ")"
			
			If s_table = "KATASTAR_PARCELE" Then
			'	Fetch-uj redove za sve atribute
				Fetch rec i_rowid From s_table
				as_broj_Parcele = s_table + ".col1"
				as_mat_broj_KO = s_table + ".col7"
				o_parc = KATASTAR_PARCELE.obj
				
				Print "Pronaðena parcela broj " + as_broj_Parcele + " (" + KO + " / " + as_mat_broj_KO + ")"
				
				Update KATASTAR_PARCELE Set ULICA = 0 Where Rowid = i_rowid
				Alter Object o_parc Info OBJ_INFO_BRUSH, b_parcela_def
				Alter Object o_parc Info OBJ_INFO_PEN, p_parcela_def
				Update KATASTAR_PARCELE Set obj = o_parc Where Rowid = i_rowid				
				
				Print "Ulica obrisana..."
				
				Exit For
			End If
		Next		
		
	End If

	Alter Button ID 312 Uncheck
	
	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "ULICA_UKLONI")
	Call ERRShow()

End Sub

Sub btn_Pronadji_Ulicu

	onerror goto Greska  
	
	dim n as smallint
	dim br as string
	
'	print chr$(12)
	print "-> Pretraga..."
	
	ReDim arr_RezultatPretrage_naziv(0)
	
	s_trazen_naziv = ReadControlValue(2)
	
	s_kom = "EXEC [mbx].[sp_ulice_pretraga_naziv] N'" + s_trazen_naziv + "'"
	
'	print s_kom
	
	' - Izvršavanje SQL StoredProc/query 
	i_hstmt = Server_Execute(i_hdbc,s_kom) 
		Server i_hstmt Bind Column 1 To s_naziv_ulice, am_stat
		Server i_hstmt Bind Column 2 To i_naziv_id, am_stat
		Server i_hstmt Fetch First

' - Kreiraj niz od ID naziva ulice				= arr_RezultatPretrage_naziv()

		n=1
		While Not Server_EOT(i_hstmt)
			ReDim arr_RezultatPretrage_naziv(n)
			arr_RezultatPretrage_naziv(n) = s_naziv_ulice		

			ReDim arr_RezultatPretrage_id(n)
			arr_RezultatPretrage_id(n) = i_naziv_id
			
			Server i_hstmt Fetch Next
			n=n+1
		Wend
	
	br = str$(n-1)
	
	Alter Control 5 Title from Variable arr_RezultatPretrage_naziv 
	Alter Control 8 Title br
	Print "-> Broj ulica koje sadrže '" + s_trazen_naziv +"' u svom nazivu:" + br 
	Alter Control 2 Active
	Server i_hstmt Close
	
	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "btn_Pronadji_Ulicu")
	Call ERRShow()
	
End Sub

Sub btn_Prikazi_Na_Mapi
	
	onerror goto Greska 
	
	Dim i_sel_ulica_id as smallint
	
	i_sel_ulica_id = arr_RezultatPretrage_id(ReadControlValue(5))
	
	Note i_sel_ulica_id

	Exit Sub
	'	-------------------------
	Greska:
	Call ERRCreate(Err(), Error$(), "btn_Prikazi_Na_Mapi")
	Call ERRShow()
	
End Sub

Sub btn_OK_PRETRAGA_ULICA

End Sub